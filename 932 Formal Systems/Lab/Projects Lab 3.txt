/* This is the ProjectsLab3 machine. */

MACHINE           ProjectsLab3

SETS              STUDENT; PROJECT

CONSTANTS         maxmark, MARK

PROPERTIES        maxmark : NAT1 & MARK = 0..maxmark

VARIABLES         students, projects, marks

INVARIANT         students <: STUDENT &
                  projects : STUDENT +-> PROJECT &
                  dom(projects) = students &
                  marks : STUDENT +-> MARK &
                  dom(marks) <: students 

INITIALISATION    students := {} || projects := {} || marks := {}

OPERATIONS
  
 signup(ss,pp) =
  PRE                   
     ss:STUDENT & pp:PROJECT & ss /: dom(projects)  
  THEN                  
     students := students \/ {ss} ||
     projects := projects \/{ss |-> pp}
  END;                  

pp <-- assignproject(ss) =
  PRE  ss:STUDENT & ss /: dom(projects) 
  THEN
     ANY ppx
       WHERE ppx : PROJECT 
       THEN pp := ppx 
            || students := students \/ {ss} 
            || projects := projects \/ {ss |-> ppx}
       END
  END;

pp <-- queryproject(ss) =
  PRE
    ss:STUDENT & ss : dom(projects)
  THEN
    pp := projects(ss)
  END;

  entermark(ss,mm) = 
   PRE
     ss:STUDENT & mm:MARK & ss : dom(projects)
   THEN
     marks := marks <+ {ss |-> mm}
   END;

mm <-- querymark(ss) =
  PRE
    ss:STUDENT & ss : dom(marks)
  THEN
    mm := marks(ss)
  END


END

=============================================================================
